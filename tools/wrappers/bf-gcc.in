#! /usr/bin/env perl

##################################################
# Wrap gcc with options for ByFl instrumentation #
# By Scott Pakin <pakin@lanl.gov>                #
##################################################

use constant DEBUG => 0;

use Cwd qw(abs_path);
use File::Basename;
use File::Copy;
use File::Spec;
use File::Temp qw(tempdir);
use File::stat;
use Getopt::Long qw(GetOptionsFromArray);
use lib '@perlsitelibdir@';   # Find ParseCompilerOpts if installed in a non-standard location.
use ParseCompilerOpts;
use v5.10.1;
use warnings;
no if $] >= 5.017011, warnings => 'experimental::smartmatch';
use strict;

# Specify some installation locations.
my $dragonegg = '@DRAGONEGG@';
my $byfl_plugin = '@plugindir@/bytesflops.so';
my $byfl_libdir = '@libdir@';
my $llvm_libdir = '@llvm_libdir@';

# Store the name of this script and the name of the underlying compiler.
my $progname = basename $0;
my $compiler = substr($progname, 3);
my $compiler_env_var = "BF_" . uc($compiler);
$compiler_env_var =~ s/\W/X/g;   # "G++" --> "GXX", e.g.
$compiler = $ENV{$compiler_env_var} if defined $ENV{$compiler_env_var};

# Let the user increase this script's verbosity.
my $verbosity = 0;

# Let the user request static analysis in addition to instrumentation.
my $static_analysis = 0;

# Optimization level requested on the command line via -O.  Note: Not
# necessary numeric (e.g., "-Os").
my $optimization_level = "0";

# Take bytesflops options from the BF_OPTS environment variable.
my @bf_options = split(" ", $ENV{"BF_OPTS"} || "");

# Let the user disable either the bytesflops plugin, DragonEgg IR
# emission, or everything besides command-line filtering.
my $bf_disable = "none";

# Apply GCC's -save-temps option to our temporary files as well.
my $save_temps = "no";

# Define a function that optionally prints, then executes a system
# command, aborting on failure.  If the first argument is "NO FAIL",
# then return an error code rather than aborting.
sub execute_command (@)
{
    my $die_on_fail = 1;
    if ($_[0] eq "NO FAIL") {
        $die_on_fail = 0;
        shift;
    }
    my @command = @_;
    if ($verbosity > 0) {
        print STDERR join(" ", map {/\s/ ? "'$_'" : $_} @command), "\n";
    }
    return $? >> 8 if system(@command) == 0 || !$die_on_fail;
    die "${progname}: Aborting\n";
}

# Define a function that removes a set of intermediate files unless
# -save-temps is specified, in which case it either leaves the files
# where they are or moves them to the current directory.
sub remove_files (@)
{
    my @deletable = map {-e $_ ? $_ : ()} @_;
    if ($save_temps eq "no") {
        # Remove intermediate files.
        print STDERR "rm -f @deletable\n" if $verbosity > 0;
        unlink(@deletable) || die "${progname}: $!\n";
    }
    elsif ($save_temps eq "cwd") {
        # Move intermediate files to the current directory.
        print STDERR "mv @deletable .\n" if $verbosity > 0;
        foreach my $fname (@deletable) {
            move($fname, ".") || die "${progname}: $!\n";
        }
    }
    else {
        # Leave intermediate files where they are.
    }
}

# Define a subroutine that initializes the default and gcc-specific
# library search paths.
my @default_lib_path;       # Linker's default search path
my @gcc_lib_path;           # Additional path elements gcc passes to ld
sub initialize_lib_paths ()
{
    # Find the default library path with the compiler and ld's help.
    my $sysroot = "/";    # Directory to substitute for "=" in a SEARCH_DIR
    if (!@default_lib_path) {
        local $/ = undef;
        open(GCCINFO, "$compiler -v -Wl,--verbose 2>&1|") || die "${progname}: $!\n";
        my $gccinfo = <GCCINFO>;
        close GCCINFO;
        $sysroot = $1 if $gccinfo =~ /\b--with-sysroot=(\S+)/s;
        if ($gccinfo =~ /\bLIBRARY_PATH=([^\n]+)/gs) {
            # Search path passed via -L flags from gcc to ld
            push @gcc_lib_path, split ":", $1;
        }
        while ($gccinfo =~ /\bSEARCH_DIR\(\"(.*?)\"\);/gs) {
            # Search path from the default linker script
            my $dirname = $1;
            $dirname =~ s/^=/$sysroot/;
            push @default_lib_path, File::Spec->canonpath($dirname);
        }
    }
}

# Define a function that finds a .a file specified with "-l<libname>"
# given <libname> and a search path (list of arguments to -L).
sub find_archive_file ($@)
{
    # Search each path in turn for the given .a file.
    my $aname = "lib" . $_[0] . ".a";
    foreach my $dirname (@_[1..$#_], @gcc_lib_path, @default_lib_path) {
        my $filespec = File::Spec->catfile($dirname, $aname);
        return $filespec if -e $filespec;
    }
    return undef;
}

# Define a function that extracts a .bc file from a .o file wrapper.
# This function returns the new filename.
sub extract_bitcode ($)
{
    # Quit early if we can.
    my $ofile = $_[0];
    my ($base, $dir, $suffix) = fileparse $ofile, "\\.[^.]+";
    return $ofile if $suffix ne ".o";
    my $bcfile = File::Spec->abs2rel(File::Spec->catfile($dir, $base . ".bc"));
    return $bcfile if -e $bcfile;     # We've already extracted the .bc file.

    # Extract the bitcode if we can.
    return $ofile if execute_command("NO FAIL",
                                     "objcopy", "--output-target=binary",
                                     "--only-section=.bitcode",
                                     $ofile, $bcfile) != 0;
    if (-z $bcfile) {
        # objcopy successfully found no .bitcode section and returned
        # a zero-byte file -- delete it and pretend objcopy failed.
        unlink $bcfile;
        return $ofile;
    }
    return $bcfile;
}

# Define a function that converts a .a file containing LLVM bitcode
# wrapped within a .o wrapper into a .bc file containing real LLVM
# bitcode.  This function returns the new filename.
sub convert_archive_file ($)
{
    # Quit early if we can.
    my $afile = $_[0];
    return $afile if -e $afile && !-w $afile;    # We can't write the file so it must not contain our bitcode wrappers.

    # Create a work directory and cd into it.
    my $workdir = tempdir($progname . "-XXXXXX",
                          DIR => ".",
                          CLEANUP => 1);
    $afile = abs_path($afile);
    chdir $workdir || die "${progname}: $!\n";
    local $SIG{"__DIE__"} = sub ($) {
        # Leave the work directory before aborting.
        chdir "..";
        die $_[0];
    };

    # Extract the contents of the .a file.
    execute_command("ar", ($verbosity > 0 ? "xv" : "x"), $afile);

    # Extract a .bc file from each .o file.
    my @bcfiles;
    foreach my $ofile (<*.o .*.o>) {
        my $bcfile = extract_bitcode $ofile;
        push @bcfiles, $bcfile if $bcfile ne $ofile;
    }

    # Convert each .bc file to a real .o file.
    foreach my $bcfile (@bcfiles) {
        execute_command("llc", $bcfile);
        my $sfile = $bcfile;
        $sfile =~ s/\.bc$/.s/;
        my $ofile = $bcfile;
        $ofile =~ s/\.bc$/.o/;
        execute_command("as", "-o", $ofile, $sfile);
    }

    # Re-archive the real .o files into a single .a file.
    execute_command("ar", $verbosity > 0 ? "rv" : "r", $afile, glob "*.o .*.o");

    # Leave the work directory so it can be removed.
    chdir "..";
    return $afile;
}

###########################################################################

# Parse the command line.
my @constructed_ARGV = (@bf_options, @ARGV);
Getopt::Long::Configure("pass_through");
GetOptionsFromArray(\@constructed_ARGV,
                    "bf-verbose+"     => \$verbosity,
                    "bf-static"       => \$static_analysis,
                    "bf-dragonegg=s"  => \$dragonegg,
                    "bf-libdir=s"     => \$byfl_libdir,
                    "bf-plugin=s"     => \$byfl_plugin,
                    "bf-disable=s"    => \$bf_disable)
    || die "${progname}: Failed to parse the command line\n";
given ($bf_disable) {
    when ("none") {
        # Disable nothing; run normally.
    }
    when ("byfl") {
        # Disable the bytesflops plugin.  (This case is handled later).
    }
    when ("bitcode") {
        # Use the DragonEgg plugin for code optimization, but generate
        # native object files.
        exit execute_command($compiler, "-fplugin=$dragonegg", grep {!m/^--?bf-/} @constructed_ARGV);
    }
    when ("dragonegg") {
        # Disable DragonEgg entirely and just use the underlying compiler.
        exit execute_command($compiler, grep {!m/^--?bf-/} @constructed_ARGV);
    }
    default {
        die "${progname}: I don't know how to disable \"$bf_disable\""
            . ' (must be one of "none", "byfl", "bitcode", or "dragonegg")' . "\n";
    }
}
@bf_options = grep {/^--?bf-/} @constructed_ARGV;
@bf_options = map {s/^--/-/; $_} @bf_options;
@bf_options = grep {!/^-bf-(verbose|static|dragonegg|libdir|disable)/} @bf_options;
my @parse_info = parse_compiler_options(grep {!/^--?bf-/} @constructed_ARGV);
my %build_type = %{$parse_info[0]};
my @target_filenames = @{$parse_info[1]};
my @compiler_opts = @{$parse_info[2]};
my @linker_opts = @{$parse_info[3]};
my @leftover_values = @{$parse_info[4]};

# Check if we're neither compiling nor linking.
if (!defined $build_type{"compile"} && !defined $build_type{"link"}) {
    # Preprocessing only -- pass all of our arguments to the compiler.
    exit execute_command(($compiler, @constructed_ARGV));
}
die "${progname}: No input files specified.  Aborting.\n" if !@constructed_ARGV;

# Define a map from source files to object files.
my %src2obj;        # Map from a source-file name to an object-file name
my @source_files;   # Subset of @leftover_values that represents source code
my @object_files;   # Subset of @target_filenames that represents object code
foreach my $fname (@leftover_values) {
    my ($base, $dir, $suffix) = fileparse $fname, "\\.[^.]+";
    push @source_files, $fname if $suffix !~ /\.[oa]$/ && $suffix ne ".so";
}
if (defined $build_type{"link"}) {
    # Linking -- object files are generated implicitly from source files.
    foreach my $srcname (@source_files) {
        my ($base, $dir, $suffix) = fileparse $srcname, "\\.[^.]+";
        push @object_files, $base . ".o";
    }
}
else {
    # Compiling only -- object files are a subset of @target_filenames.
    @object_files = grep {/\.o$/} @target_filenames;
}
if ($#source_files != $#object_files) {
    my $vs = sprintf "%d vs. %d", 1+$#source_files, 1+$#object_files;
    die "${progname}: Source-file list and object-file list contain different number of elements ($vs)";
}
foreach my $i (0 .. $#source_files) {
    $src2obj{$source_files[$i]} = $object_files[$i];
}

# Determine if we should keep our intermediate files and if so, where
# to put them.
foreach my $opt_arg (@compiler_opts, @linker_opts) {
    my ($opt, $arg) = @$opt_arg;
    if ($opt eq "save-temps") {
        if (!defined $arg) {
            $save_temps = "cwd";
        }
        elsif ($arg eq "cwd") {
            $save_temps = "cwd";
        }
        elsif ($arg eq "obj") {
            $save_temps = "obj";
        }
        else {
            $save_temps = "no";
        }
    }
}

# If we're compiling, then add options to invoke the DragonEgg plugin
# and produce LLVM IR.
if (defined $build_type{"compile"}) {
    # Construct a command line.
    my @command_line = ($compiler,
                        "-g",
                        "-fplugin=$dragonegg",
                        "-fplugin-arg-dragonegg-emit-ir");
    foreach my $opt_arg (@compiler_opts) {
        my ($opt, $arg) = @$opt_arg;
        if (defined $arg) {
            # The option has an argument.
            if ($opt eq "O") {
                push @command_line, "-$opt$arg";
                $optimization_level = $arg;
            }
            elsif ($opt eq "g") {
                push @command_line, "-$opt$arg";
            }
            elsif ($opt eq "o") {
                # Ignore -o.  We'll be generating intermediate bitcode
                # files and later producing object files from those.
            }
            elsif ($opt =~ /^([xIJ]|M[FTQ])$/) {
                # A few options do not accept an equals sign; they
                # require a space.
                push @command_line, ("-$opt", $arg);
            }
            else {
                push @command_line, "-$opt=$arg";
            }
        }
        else {
            # The option does not have an argument.
            if ($opt eq "c" || substr($opt, 0, 1) eq "g") {
                # Ignore -c.  We'll soon be replacing it with -S.
                # Ignore -g.  We'll be adding it in explicitly.
            }
            else {
                push @command_line, "-" . $opt;
            }
        }
    }
    push @command_line, "-S";    # Override options that conflict with -S.
    push @command_line, @source_files;

    # Execute the command line.
    execute_command @command_line;

    # Run the Byfl LLVM pass on each bitcode file generated.  Then,
    # very sneakily embed the bitcode within a .o file so as not to
    # confuse the invoking Makefile.
    foreach my $srcfile (@source_files) {
        # Prepare to run the Byfl LLVM pass.
        my $ofile = $src2obj{$srcfile};
        my $sfile = basename $srcfile;
        $sfile =~ s/\.[^.]+$/.s/;
        my $bcfile = $sfile;
        $bcfile =~ s/\.s$/.bc/;
        my $opt_bcfile = $bcfile;
        $opt_bcfile =~ s/\.bc$/.opt.bc/;
        my $byfl_bcfile = $bcfile;
        $byfl_bcfile =~ s/\.bc$/.byfl.bc/;
        my @deletable = ($sfile, $bcfile, $opt_bcfile, $byfl_bcfile);

        # Handle the obscure special case of the same input file being
        # specified multiple times on the same command line.  In this
        # case, the .s file is deleted/moved after the first time the
        # input file is processed, which would cause opt to fail to
        # find its input file and therefore abort.
        next if !-e $sfile;

                # add --debug-pass=Structure to debug
        # Run the static analyzer if -bf-static was specified.
        execute_command("opt", "-load", "$byfl_plugin",
                        "-bytesflops", @bf_options,
                        "-analyze", "$sfile") if $static_analysis;

        # Unfortunately, DragonEgg's bitcode generation apparently
        # runs *before* GCC code optimizations.  This implies that
        # Byfl is analyzing unoptimized code, which is likely to
        # produce substantially different output from
        # binary-instrumentation tools.  We therefore explicitly
        # run an optimization pass before Byfl instrumentation if
        # any optimizations are specified on the command line.
        my $infile = $sfile;
        my $optlevel = $optimization_level;
        if ($optimization_level ne "0") {
            # Make a few modifications to the optimization level
            # specified on the command line for opt's benefit.
            # Specifically, replace "-Ofast" and any "-O<n>" for n>3
            # with "-O3".
            $optlevel = "3" if $optlevel eq "fast" || ($optlevel =~ /^(\d+)$/ && $1 > 3);
            execute_command("opt", "-O$optlevel",
                            $infile, "-o", $opt_bcfile);
            $infile = $opt_bcfile;
        }

        # Instrument with Byfl then optimize the result using the LLVM
        # optimizer at -O3 or higher (unless -Os or some other
        # non-numeric optimization level is specified).
        if ($bf_disable eq "byfl") {
            # Disable the bytesflops plugin if so instructed.
            if ($optimization_level eq "0") {
                execute_command("llvm-as", "-o", $bcfile, $sfile);
            }
            else {
                print STDERR "mv $opt_bcfile $bcfile\n" if $verbosity > 0;
                rename($opt_bcfile, $bcfile) || die "${progname}: $!\n";
            }
        }
        else {
            # Normally, we run Byfl instrumentation.
            execute_command("opt", "-load", "$byfl_plugin",
                            "-bytesflops", @bf_options,
                            $infile, "-o", $byfl_bcfile);
            if ($optlevel =~ /^\d+$/ && $optlevel ne "0" ) {
                # Numeric optimization level -- round to -O3 or higher
                # then run the bitcode through opt.
                my $post_byfl_opt = $optlevel;
                $post_byfl_opt = "3" if $post_byfl_opt < 3;
                execute_command("opt", "-O$post_byfl_opt",
                                $byfl_bcfile, "-o", $bcfile);
            }
            else {
                # Non-numeric optimization level -- don't run opt.
                print STDERR "mv $byfl_bcfile $bcfile\n" if $verbosity > 0;
                rename($byfl_bcfile, $bcfile) || die "${progname}: $!\n";
            }
        }

        # Embed the LLVM bitcode within a .o file.
        execute_command($compiler, "-o", $ofile, "-c", "-x", "c", "/dev/null");
        execute_command("objcopy", "--add-section", ".bitcode=$bcfile",
                        "--set-section-flags", ".bitcode=alloc", $ofile);

        # Remove the .s and .bc files.
        remove_files(@deletable);
    }
}

# If we're linking, extract LLVM bitcode from all of our object files
# and replace ld options with clang options.
if (defined $build_type{"link"}) {
    # Convert the linker options from gcc/ld to clang.
    my @llvm_ld_options;     # Options to pass to clang
    my @Ldirs;               # Directories listed with -L
    my @lfiles;              # Libraries listed with -l
    foreach my $opt_arg (@linker_opts) {
        my ($opt, $arg) = @$opt_arg;
        if ($opt eq "L") {
            push @Ldirs, $arg;
            push @llvm_ld_options, "-L$arg";
        }
        elsif ($opt eq "l") {
            push @lfiles, $arg;
        }
        elsif ($opt eq "o") {
            push @llvm_ld_options, ("-o", $arg);
        }
        elsif ($opt eq "rdynamic") {
        }
        elsif ($opt eq "O") {
            # Make a few modifications to the optimization level
            # specified on the command line for clang's benefit.
            # Specifically, "-Ofast" with "-O3" and any "-O<n>" for
            # n>4 with "-O4".
            if ($arg eq "fast") {
                $arg = "3";
            }
            elsif ($arg =~ /^(\d+)$/ && $1 > 4) {
                $arg = "4";
            }
            push @llvm_ld_options, "-$opt$arg";
        }
        elsif ($opt =~ /^(pg|fopenmp)$/) {
            push @llvm_ld_options, "-$opt";
        }
        elsif ($opt =~ /^(march|mtune)$/) {
            push @llvm_ld_options, "-$opt=$arg";
        }
        else {
            push @llvm_ld_options, ("-Xlinker", "-$opt");
            push @llvm_ld_options, ("-Xlinker", "$arg") if defined $arg;
        }
    }

    # Add a few default options.  We explicitly add the compiler's
    # default library search path because it will likely differ from
    # clang's.  Also, note that we explicitly add "-L$byfl_libdir".
    # This is needed to resolve the library dependence specified in
    # bytesflops.cpp.
    initialize_lib_paths();
    foreach my $dirname (@gcc_lib_path) {
        push @llvm_ld_options, "-L" . $dirname;
    }
    push @llvm_ld_options, ("-L$byfl_libdir", "-L$llvm_libdir", "-lm");
    if ($bf_disable eq "none") {
        push @llvm_ld_options, ("-rpath", $byfl_libdir, "-lbyfl");
        push @llvm_ld_options, "-lpthread" if grep {/^-bf-thread-safe$/} @bf_options;
    }
    push @llvm_ld_options, "-lstdc++" if $progname eq "bf-g++";
    push @llvm_ld_options, "-lgfortran" if $progname eq "bf-gfortran";
    push @llvm_ld_options, ("-lgobegin", "-lgo") if $progname eq "bf-gccgo";

    # Construct a list of linker inputs.
    my @llvm_ld_inputs;    # Ordered list of .a and .o files.
    foreach my $fname (@leftover_values) {
        my $gen_o = $src2obj{$fname};     # A .o file generated from source
        if (defined $gen_o) {
            push @llvm_ld_inputs, $gen_o;
        }
        elsif ($fname =~ /\.a$/) {
            my $bcafile = convert_archive_file $fname;
            if ($bcafile =~ /\.bc$/) {
                # Instrumented library
                push @llvm_ld_inputs, $bcafile;
            }
            else {
                # Uninstrumented library
                push @llvm_ld_options, $fname;
            }
        }
        else {
            push @llvm_ld_inputs, $fname;
        }
    }
    @llvm_ld_inputs = map {extract_bitcode $_} @llvm_ld_inputs;

    # Add all libraries specified with -l to the linker inputs.
    foreach my $lfile (@lfiles) {
        my $afile = find_archive_file($lfile, @Ldirs);
        if (!defined $afile) {
            push @llvm_ld_options, "-l$lfile";
            next;
        }
        my $bcafile = convert_archive_file $afile;
        if ($bcafile =~ /\.bc$/) {
            push @llvm_ld_inputs, $bcafile;
        }
        else {
            push @llvm_ld_options, "-l$lfile";
        }
    }

    # Run the linker.
    execute_command("clang", @llvm_ld_inputs, @llvm_ld_options);

    # Clean up all of the .bc files we created.
    remove_files(grep(/\.bc$/, @llvm_ld_inputs));
}

###########################################################################

__END__

=head1 NAME

bf-gcc - Inject Byfl instrumentation while compiling a program

=head1 SYNOPSIS

bf-gcc
[B<-bf-by-func>]
[B<-bf-call-stack>]
[B<-bf-data-structs>]
[B<-bf-types>]
[B<-bf-inst-mix>]
[B<-bf-inst-deps>]
[B<-bf-vectors>]
[B<-bf-unique-bytes>]
[B<-bf-mem-footprint>]
[B<-bf-strides>]
[B<-bf-every-bb>]
[B<-bf-merge-bb>=I<count>]
[B<-bf-reuse-dist>[=loads|stores]
[B<-bf-include>=I<function>[,I<function>]...]
[B<-bf-exclude>=I<function>[,I<function>]...]
[B<-bf-thread-safe>]
[B<-bf-verbose>]
[B<-bf-static>]
[B<-bf-dragonegg>=I<path/to/dragonegg.so>]
[B<-bf-libdir>=I<path/to/byfl/lib/>]
[B<-bf-plugin>=I<path/to/bytesflops.so>]
[B<-bf-disable>=I<feature>]
[I<gcc_options>...]
[I<file>...]

=head1 DESCRIPTION

B<bf-gcc> is the Byfl project's C compiler.  It compiles C code,
instrumenting it to report various I<software> performance counters at
execution time.  Software performance counters are analogous to the
hardware performance counters provided by modern processors but
measure program execution in a hardware-independent fashion.  That is,
users can expect to observe the same measurements across different
processor architectures.

B<bf-gcc> works by using the GCC DragonEgg plugin to convert GCC's
internal representation to LLVM's, instrumenting that with Byfl, and
finally using Clang to generate a native executable.

=head1 OPTIONS

=over 4

=item B<-bf-by-func>

Report performance counters for each function individually.

=item B<-bf-call-stack>

Report performance counters for each unique call stack.

=item B<-bf-data-structs>

Report loads and stores on a per-data-structure basis.

=item B<-bf-types>

Tally the number of times each data type is loaded or stored.

=item B<-bf-inst-mix>

Tally the number of times each instruction type was executed.

=item B<-bf-inst-deps>

Tally what instructions feed into what other instructions.

=item B<-bf-vectors>

Report information about the number and type of vector operations
performed.

=item B<-bf-unique-bytes>

Report the number of unique memory addresses referenced.

=item B<-bf-mem-footprint>

Report the memory capacity requires to hold various percentages of the
dynamic memory accesses.

=item B<-bf-strides>

Bin the stride sizes observes by each load and store.

=item B<-bf-every-bb>

Report performance counters at the basic-block level.

=item B<-bf-merge-bb>=I<count>

Aggregate basic blocks into groups of I<count> to reduce the output
volume.

=item B<-bf-reuse-dist>[=loads|stores]

Track data reuse distance.  With an argument of C<loads>, only loads
are tracked.  With an argument of C<stores>, only stores are tracked.
With no argument -- or with an argument of C<loads,stores>) -- both
loads and stores are tracked.

=item B<-bf-include>=I<function>[,I<function>]...

Instrument only the specified functions.

=item B<-bf-exclude>=I<function>[,I<function>]...

Do not instrument the specified functions.

=item B<-bf-thread-safe>

Prevent corruption caused by simultaneous accesses to the same set of
performance counters.

=item B<-bf-verbose>

Make B<bf-gcc> output all of the helper programs it calls.

=item B<-bf-static>

Output static instruction counts at compile time.

=item B<-bf-dragonegg>=I<path/to/dragonegg.so>

Point B<bf-gcc> to the DragonEgg plugin.

=item B<-bf-libdir>=I<path/to/byfl/lib/>

Point B<bf-gcc> to the directory containing the Byfl library
(F<libbyfl.a> or F<libbyfl.so>).

=item B<-bf-plugin>=I<path/to/bytesflops.so>

Point B<bf-gcc> to the Byfl plugin (C<bytesflops.so>).

=item B<-bf-disable>=I<feature>

Disable certain aspects of B<bf-gcc>'s operation.

=back

In addition, B<bf-gcc> accepts all of the common B<gcc> options.

=head1 EXAMPLES

The simplest usage of B<bf-gcc> is to compile just like with B<gcc>:

    bf-gcc -O2 -g -o myprog myprog.c

The resulting F<myprog> executable will output a basic set of
performance information at the end of the run.

More performance information can be requested -- at the cost of slower
execution and a larger memory footprint:

    bf-gcc -bf-by-func -bf-types -bf-inst-mix -bf-vectors \
      -bf-mem-footprint -O2 -g -o myprog myprog.c

=head1 ENVIRONMENT

=over 4

=item C<BF_OPTS>

Provide a space-separated list of B<bf-gcc> command-line arguments.

=item C<BF_PREFIX>

Prefix each line of output with the contents of the C<BF_PREFIX>
environment variable.

=item C<BF_BINOUT>

Specify the name of a C<.byfl> file to which to write detailed Byfl
output in binary format.

=item C<BF_GCC>

Wrap the specified compiler instead of B<gcc>.

=back

C<BF_OPTS> is used at compile time.  Command-line arguments take
precedence over those read from the C<BF_OPTS> environment variable.
The advantage of using the environment variable, however, is that a
user can rebuild a project with different sets of performance counters
without having to modify the project's F<Makefile>s (or analogue in
another build system) beyond an initial modification to use B<bf-gcc>
as the C compiler.

C<BF_PREFIX> is used at run time.  An important characteristic of the
C<BF_PREFIX> environment variable is that it honors POSIX shell-style
variable expansions.  For example, if C<BF_PREFIX> is set to the
string C<Rank ${OMPI_COMM_WORLD_RANK}>, then a line that would
otherwise begin with C<BYFL_SUMMARY:> will instead begin with C<Rank 3
BYFL_SUMMARY:>, assuming that the C<OMPI_COMM_WORLD_RANK> environment
variable has the value C<3>.

Although the characters C<|>, C<&>, C<;>, C<E<lt>>, C<E<gt>>, C<(>,
C<)>, C<{>, and C<}> are not normally allowed within C<BF_PREFIX>,
C<BF_PREFIX> does support backquoted-command evaluation, and the child
command can contain those characters, as in

    BF_PREFIX='`if true; then (echo YES; echo MAYBE); else echo NO; fi`'

(which prefixes each line with C<YES MAYBE>).

As a special case, if C<BF_PREFIX> expands to a string that begins
with C</> or C<./>, it is treated not as a prefix but as a filename.
The Byfl-instrumented executable will redirect all of its Byfl output
to that file instead of to the standard output device.

C<BF_BINOUT> is also used at run time.  Like C<BF_PREFIX>, it honors
POSIX shell-style variable expansions.  If C<BF_BINOUT> is set to the
empty string, no binary output file will be produced.

=head1 NOTES

=head2 Explanation of command-line options

When B<-bf-call-stack> is specified, a function I<F> is reported
separately when called from function I<A> and when called from
function I<B>).  B<-bf-call-stack> overrides B<-bf-by-func>.

For the purposes of B<-bf-data-structs>, Byfl defines a data structure
as either a statically allocated block of memory (which has a name in
the executable's symbol table) or a collection of data blocks
dynamically allocated from the same program call point (i.e.,
instruction address).  Byfl assigns the latter a name based on a
textual description of the call point.

The B<-bf-types> option tallies, for example, the number of loads of
64-bit floating-point values as distinct from loads of 64-bit unsigned
integer values.

See the L<LLVM Language Reference
Manual|http://llvm.org/docs/LangRef.html> for descriptions of the
instructions that are tallied by B<-bf-inst-mix>.

B<-bf-inst-deps> tallies each instruction with the instructions that
produced its first two operands.  (Ellipses are used to indicate that
the instruction takes more than two operands.)  For example,
C<Xor(Add, Mul)> represents an exclusive OR with one operand being the
result of a previous integer addition and one being the result of a
previous integer multiplication (i.e., C<A = (B + C) XOR (D * E)>).

Use of B<-bf-unique-bytes> consumes one bit of memory per unique
address referenced by the program.

Use of B<-bf-mem-footprint> consumes S<8 bytes> of memory per unique
address referenced by the program.

A I<basic block> is a unit of code that can be entered only at the
first instruction and that branches only at the last instruction.
Because basic blocks tend to be small, B<-bf-every-bb> produces a
substantial amount of output for typical programs.  It is recommended
that B<-bf-every-bb> always be used in conjunction with
B<-bf-merge-bb> to reduce the amount of information output.

The B<-bf-static> option currently lists the static number of loads,
stores, floating-point operations, conditional and indirect branches,
total operations, and basic blocks for each function and the number of
instructions in each inner loop detected.

The B<-bf-disable> option is quite useful for troubleshooting.  Its
option can be one of the following:

=over 4

=item C<none>

Don't disable any features (the default).

=item C<byfl>

Disable the Byfl plugin (i.e., inject no instrumentation into the code).

=item C<bitcode>

Use the DragonEgg plugin for code generation, but generate native
object files.

=item C<dragonegg>

Disable DragonEgg entirely -- and hence all B<bf-gcc> functionality -- and
transfer control to the underlying B<gcc> compiler.

=back

That is, if B<bf-gcc> fails to compile or link an application, try
disabling first C<byfl>, then C<bitcode>, and finally C<dragonegg> to
identify where the problem lies.

The Byfl plugin proper (F<bytesflops.so>) honors all of the
command-line options listed above except B<-bf-verbose>,
B<-bf-static>, and B<-bf-disable>.  Those options are specific to the
B<bf-gcc> script.

=head2 Selective instrumentation

The simplest way to instrument only part of a program is at the module
level.  That is, compile the "interesting" modules with B<bf-gcc> and
the rest with B<gcc> (and link with B<bf-gcc> to pull in the Byfl
run-time library).  However, B<bf-gcc> also supports inserting
programmer-defined "calipers" into the code.  These can not only
selectively enable and disable performance counters but can also
distinguish blocks of code with a program-defined tag.  To enable this
feature, an application must define a function with the following
C-language prototype:

    const char* bf_categorize_counters (void);

That is, C<bf_categorize_counters()> takes no arguments and returns a
short tag describing the current phase of the application.  A return
value of C<NULL> disables logging of performance counters.

Application developers should be aware of the following caveats
regarding C<bf_categorize_counters()>:

=over 4

=item *

C<bf_categorize_counters()> should be written to execute quickly
because it will be invoked extremely frequently (once per basic
block).  Consequently, a typical definition is for
C<bf_categorize_counters()> simply to return a global variable and for
the application to assign to that global variable at various points in
the code.

=item *

C<bf_categorize_counters()> works only when B<-bf-every-bb> is
specified.  (B<bf-gcc> issues a warning message if the function is
defined but the option is not specified.)  If the user is not
interested in seeing per-basic-block counters, these can be
effectively disabled by specified a large argument to B<-bf-merge-bb>
(e.g., C<-bf-merge-bb=18446744073709551615>).

=item *

Because B<bf-gcc> instruments code at compile time while
C<bf_categorize_counters()> works at run time, the implication is that
returning C<NULL> still pays a performance penalty relative to
uninstrumented code.

=back


=head2 Compiler optimizations

B<bf-gcc> first optimizes using the user-specified optimization level
(e.g., B<-O2>), then instruments the code with Byfl, and finally
optimizes again at B<-O3> to speed up execution.  This final
optimization step does not affect the counter values that Byfl
reports.  As a special case, however, compiling with C<-O0> disables
the final optimization step as well.  This can be used to help debug
Byfl itself.

=head1 FILES

B<bf-gcc> is a script that invokes a number of other programs: B<ar>,
B<as>, B<clang>, B<gcc> (with the F<dragonegg.so> plugin), B<llc>,
B<llvm-as>, B<objcopy>, and B<opt>.

=head1 BUGS

Thread safety is still quite premature.  Even with
B<-bf-thread-safe>, instrumented code is likely to crash.

=head1 AUTHOR

Scott Pakin, I<pakin@lanl.gov>

=head1 SEE ALSO

gcc(1), clang(1),
L<the Byfl home page|https://github.com/lanl/Byfl>
